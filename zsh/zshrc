# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.zsh

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load everything but the path and completion files
for file in ${config_files:#*/*completion.zsh}
do
  source $file
done

# add completions to path
fpath=($HOME/.zsh/completions $fpath)

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/*completion.zsh}
do
  source $file
done

unset config_files


[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# source local zsh for a directory (project specific code)
if [[ -f .local.zsh && -r .local.zsh ]]; then
  source .local.zsh
fi

# hook after changing directory
function chpwd() {
  # source local zsh for a directory (project specific code)
  if [[ -f .local.zsh && -r .local.zsh ]]; then
    source .local.zsh
  fi
}

typeset -U path

# Favor Homebrew binaries over system
path=($HOME/.yarn/bin $HOME/.cargo/env node_modules/.bin /usr/local/bin /usr/local/sbin $GOPATH/bin $GOROOT/bin $path)

. $HOME/.opam/opam-init/init.zsh > /dev/null 2> /dev/null || true

eval $(opam config env)
